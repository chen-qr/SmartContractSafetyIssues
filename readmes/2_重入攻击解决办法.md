# 重入攻击解决办法

## 1. 先更新数据、再外部调用

这个方式，靠开发者个人自觉习惯，其实也没有很安全。

```solidity
pragma solidity ^0.8.0;

contract SafeContract {
    mapping(address => uint256) public balances;

    function deposit() public payable {
        balances[msg.sender] += msg.value;
    }

    // ✅正确代码：先更新数据、再外部调用
    function withdraw(uint256 amount) public {
        require(balances[msg.sender] >= amount, "Insufficient balance");

        // 先更新余额
        balances[msg.sender] -= amount;

        // 再发送以太币
        (bool success, ) = msg.sender.call{value: amount}("");
        require(success, "Transfer failed");
    }

    // ❌错误代码：先外部调用、采取更新余额，会被重入攻击，造成余额损失
    function withdraw(uint256 amount) public {
        require(balances[msg.sender] >= amount, "Insufficient balance");

        // 发送以太币给请求者
        (bool success, ) = msg.sender.call{value: amount}("");
        require(success, "Transfer failed");

        // 更新余额
        balances[msg.sender] -= amount;
    }
}
```

## 使用ReentrancyGuard修饰符

ReentrancyGuard修饰符是OpenZeppelin提供的一个工具，用于防止重入攻击。

它通过在合约函数开始和结束时设置一个状态变量来实现，这个变量在函数执行期间**锁定**，从而阻止同一合约的嵌套调用。类似于传统多线程开发中的互斥锁。

ReentrancyGuard 使用：

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

// 第一步，使用 ReentrancyGuard
contract SafeContract is ReentrancyGuard {
    mapping(address => uint256) public balances;

    function deposit() public payable {
        balances[msg.sender] += msg.value;
    }

    // 第二步，使用 nonReentrant
    function withdraw(uint256 amount) public nonReentrant {
        require(balances[msg.sender] >= amount, "Insufficient balance");
        balances[msg.sender] -= amount;
        (bool success, ) = msg.sender.call{value: amount}("");
        require(success, "Transfer failed");
    }
}
```

ReentrancyGuard 原理：

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract ReentrancyGuard {
    // 状态变量 `_notEntered` 用于跟踪是否进入互斥锁状态
    bool private _notEntered;

    constructor() {
        // 初始状态为未进入
        _notEntered = true;
    }

    // `nonReentrant` 修饰符用于防止重入
    modifier nonReentrant() {
        // 在第一次调用时，_notEntered 应该是 true
        require(_notEntered, "ReentrancyGuard: reentrant call");

        // 在第一次调用后，设置 _notEntered 为 false
        _notEntered = false;

        // 执行修饰的函数
        _;

        // 在函数执行完成后，重置 _notEntered 为 true
        _notEntered = true;
    }
}
```